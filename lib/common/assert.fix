// Assert functions.
module Minilib.Common.Assert;

// `assert_lazy(errmsg, value, lazy_func)` asserts that a condition (boolean value) is true.
// If the assertion failed, prints a message to the stderr and aborts the program.
// If the assertion succeeded, calls a lazy function.
// 
// For example, instead of 
// `eval *assert(|_| "error", a == b); ...` in an IO monad, you can write
// `assert_lazy(|_| "error", a == b) $ |_| ...`.
assert_lazy: Lazy String -> Bool -> Lazy a -> a;
assert_lazy = |errmsg, value, lazy_func| (
    if !value { undefined("Assertion failed!: " + errmsg()) };
    lazy_func()
);

// `assert_eq_lazy(errmsg, lhs, rhs, lazy_func)` asserts that two values are equal.
// If the assertion failed, prints a message to the stderr and aborts the program.
// If the assertion succeeded, calls a lazy function.
// For example, instead of 
// `eval *assert_eq(|_| "error", a, b); ...` in an IO monad, you can write
// `assert_eq_lazy(|_| "error", a, b) $ |_| ...`.
assert_eq_lazy : [a : Eq] Lazy String -> a -> a -> Lazy b -> b;
assert_eq_lazy = |errmsg, lhs, rhs, lazy_func| (
    if lhs != rhs { undefined("Assertion failed!: " + errmsg()) };
    lazy_func()
);
