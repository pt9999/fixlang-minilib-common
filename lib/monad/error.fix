// Definition of `MonadErrorIF` trait which can report errors.
module Minilib.Monad.Error;

// A trait for the interface of monads which can report errors.
trait MonadError = Monad + MonadErrorIF;

// A trait for monads which can report errors.
trait [m: * -> *] m: MonadErrorIF {
    // `error(e)` throws an error.
    error: ErrMsg -> m a;

    // `ma.catch(handler)` catches any error that is thrown during the computation of `ma`.
    catch: (ErrMsg -> m a) -> m a -> m a;
}

// Returns a monad which performs `body` first, then `cleanup`,
// regardless of whether `body` throws an error.
//
// If `cleanup` throws an error, that error takes precedence over an error thrown by `body`.
//
// # Parameters
// - `cleanup`: a cleanup
// - `body`: a body
finally: [m: MonadError] m () -> m a -> m a;
finally = |cleanup, body| (
    let a = *body.catch(|errmsg|
        cleanup;;
        error(errmsg)
    );
    cleanup;;
    pure $ a
);


impl Result ErrMsg: MonadErrorIF {
    error = Result::err;
    catch = |handler, res| (
        if res.is_err { handler(res.as_err) };
        res
    );
}

impl IOFail: MonadErrorIF {
    error = IOFail::throw;
    catch = |handler, iof| (
        let res = *iof.to_result.lift;
        if res.is_err { handler(res.as_err) };
        res.from_result
    );
}

// Lifts an operation result to a monad.
lift_result: [m: MonadError] Result ErrMsg a -> m a;
lift_result = |res| (
    if res.is_err { error $ res.as_err };
    pure $ res.as_ok
);

// Synonym of `lift_result`.
from_result_t: [m: MonadError] Result ErrMsg a -> m a;
from_result_t = lift_result;

// Converts to an operation result.
to_result_t: [m: MonadError] m a -> m (Result ErrMsg a);
to_result_t = |ma| (
    ma.bind(ok >> pure).catch(err >> pure)
);

