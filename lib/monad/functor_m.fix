// `map_m`, `foreach_m` for Array and Iterator.
module Minilib.Monad.FunctorM;

trait [f : *->*] f : FunctorM {
    // Maps a functor in a monad context.
    // Similar to Haskell's `mapM` function.
    //
    // Example:
    // ```
    // let ios = ...Array of IO operations...;
    // let tasks = *ios.map_m(AsyncIOTask::make);
    // let results = *tasks.map_m(get);
    // ==> (tasks are created and started in parallel, then the results of tasks are collected)
    // ```
    map_m : [m : Monad] (a -> m b) -> f a -> m (f b);
    // Maps a functor in a monad context ignoring the results.
    // Similar to Haskell's `mapM` function, but the results are discarded.
    //
    // Example:
    // ```
    // ["hello", "world"].foreach_m(println);;
    // ==> ("hello\nworld\n" is printed)
    // ```
    foreach_m : [m : Monad] (a -> m ()) -> f a -> m ();
}

impl Array: FunctorM {
    map_m = |f, array| array.to_iter.fold_m(
        Array::empty(array.get_size), |output, a|
        let b = *f(a);
        pure $ output.push_back(b)
    );
    foreach_m = |f, array| array.to_iter.fold_m(
        (), |_, a|
        f(a)
    );
}

impl Iterator: FunctorM {
    map_m = |f, iter| (
        let output = *iter.to_array.map_m(f);
        pure $ output.to_iter
    );
    foreach_m = |f, iter| iter.fold_m(
        (), |_, a|
        f(a)
    );
}

