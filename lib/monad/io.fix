// This module defines the `MonadIO` and `MonadIOFail` traits, which can lift `IO` and `IOFail` monad.
module Minilib.Monad.IO;

// A monadic trait which can lift IO monad.
trait MonadIO = Monad + MonadIOIF;

// An interface of a monadic trait which can lift IO monad.
trait [m: * -> *] m: MonadIOIF {
    lift_io: IO a -> m a;
}

// A monadic trait which can lift IO and IOFail monad.
trait MonadIOFail = Monad + MonadIOIF + MonadIOFailIF;

// An interface of a monadic trait which can lift IOFail monad.
trait [m: * -> *] m: MonadIOFailIF {
    lift_iofail: IOFail a -> m a;
}

impl IO: MonadIOIF {
    lift_io = |io| io;
}

impl IOFail: MonadIOIF {
    lift_io = IOFail::lift;
}

impl IOFail: MonadIOFailIF {
    lift_iofail = |iofail| iofail;
}

// Same as `println`, except that it can be automatically lifted into monads that implement MonadIO.
mprintln: [m: MonadIO] String -> m ();
mprintln = println >> lift_io;

// Same as `eprintln`, except that it can be automatically lifted into monads that implement MonadIO.
emprintln: [m: MonadIO] String -> m ();
emprintln = eprintln >> lift_io;
