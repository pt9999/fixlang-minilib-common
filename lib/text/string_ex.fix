// String utility functions.
//
// Features:
// - Array U8 -> String conversion
// - Search, replace, split, comparison of String
// - Format
module Minilib.Text.StringEx;

import Character;

//---------------------------------
// String utility functions
//---------------------------------

// Converts a byte array to a string. Specifically, it calls `String::_unsafe_to_string()`
// after appending a null character to the end of the byte array.
//
// # Parameters
// * `bytes` - a byte array
//
// Example:
// ```
// [ 'a', 'b', 'c' ]._unsafe_to_string
// ==> "abc"
// [ 0x41_U8, 0x42_U8, 0x43_U8 ]._unsafe_to_string
// ==> "ABC"
// ```
_unsafe_to_string: Array U8 -> String;
_unsafe_to_string = |bytes| (
    String::_unsafe_from_c_str (bytes.push_back(0_U8))
);

// Converts the specified string to lowercase.
//
// NOTE: This function is affected by the locale.
//
// # Parameters
// * `str` - a string
//
// Example:
// ```
// "ABCdef123".to_lower
// ==> "abcdef123"
// ```
to_lower: String -> String;
to_lower = |str| (
    str.get_bytes.pop_back.map(Character::to_lower)._unsafe_to_string
);

// Converts the specified string to uppercase.
//
// NOTE: This function is affected by the locale.
//
// # Parameters
// * `str` - a string
//
// Example:
// ```
// "ABCdef123".to_upper
// ==> "ABCDEF123"
// ```
to_upper: String -> String;
to_upper = |str| (
    str.get_bytes.pop_back.map(Character::to_upper)._unsafe_to_string
);

// Deprecated: Please use `Std::String::from_U8`.
//
// Converts a byte (a character) to a string of length 1.
//
// Example:
// ```
// 0x41_U8.byte_to_string
// ==> "A"
// ```
byte_to_string: U8 -> String;
byte_to_string = |c| (
    Array::empty(2).push_back(c)._unsafe_to_string
);

// Creates a string of the specified length, where each byte is equal to the specified byte.
//
// # Parameters
// * `size` - the length of the string
// * `c` - a byte (a character)
//
// Example:
// ```
// StringEx::fill(5, 'A')
// ==> "AAAAA"
// ```
fill: I64 -> U8 -> String;
fill = |size, c| (
    let size = max(0, size);
    Array::fill(size, c)._unsafe_to_string
);

// Searches for the specified byte from the beginning of a string.
// If found, returns the index of that byte.
//
// # Parameters
// * `c` - a byte (a character)
// * `str` - a string
//
// Example:
// ```
// "aaa/bbb/ccc".find_byte('/')
// ==> some(3)
// "aaa".find_byte('/')
// ==> none()
// ```
find_byte: U8 -> String -> Option I64;
find_byte = |c, str| (
    let array = str.get_bytes;
    let n = str.get_size;
    loop(
        0, |i|
        if i >= n {
            break $ none()
        };
        if array.@(i) == c {
            break $ some $ i
        };
        continue $ i + 1
    )
);

// Gets the prefix of a string up to a specified length.
//
// # Parameters
// * `size` - the length of the prefix
// * `str` - a string
//
// Example:
// ```
// "ABCDE".get_prefix(3)
// ==> "ABC"
// "ABCDE".get_prefix(10)
// ==> "ABCDE"
// ```
get_prefix: I64 -> String -> String;
get_prefix = |size, str| (
    let size = max(0, size);
    str.get_sub(0, min(size, str.get_size))
);

// Gets the suffix of a string up to a specified length.
//
// # Parameters
// * `size` - the length of the suffix
// * `str` - a string
//
// Example:
// ```
// "ABCDE".get_suffix(3)
// ==> "CDE"
// "ABCDE".get_suffix(10)
// ==> "ABCDE"
// ```
get_suffix: I64 -> String -> String;
get_suffix = |size, str| (
    let size = max(0, size);
    str.get_sub(max(0, str.get_size - size), str.get_size)
);

// Searches for the specified byte from the end of a string.
// If found, returns the index of that byte.
//
// # Parameters
// * `c` - a byte (a character)
// * `str` - a string
//
// Example:
// ```
// "aaa/bbb/ccc".find_last_byte('/')
// ==> some(7)
// "aaa".find_last_byte('/')
// ==> none()
// ```
find_last_byte: U8 -> String -> Option I64;
find_last_byte = |c, str| (
    let array = str.get_bytes;
    let n = str.get_size;
    loop(
        n - 1, |i|
        if i < 0 {
            break $ none()
        };
        if array.@(i) == c {
            break $ some $ i
        };
        continue $ i - 1
    )
);

// Pads on the left with the specified byte until it reaches the specified length.
//
// # Parameters
// * `size` - the minimum length of the padded string
// * `c` - a byte (a character)
// * `str` - a string
//
// Example:
// ```
// "123".pad_left(5, '0')
// ==> "00123"
// "1234567".pad_left(5, '0')
// ==> "1234567"
// ```
pad_left: I64 -> U8 -> String -> String;
pad_left = |size, c, str| (
    let size = max(0, size);
    if size <= str.get_size { str };
    StringEx::fill(size - str.get_size, c) + str
);

// Pads on the right with the specified byte until it reaches the specified length.
//
// # Parameters
// * `size` - the minimum length of the padded string
// * `c` - a byte (a character)
// * `str` - a string
//
// Example:
// ```
// "abc".pad_right(5, ' ')
// ==> "abc  "
// "abcdefg".pad_right(5, ' ')
// ==> "abcdefg"
// ```
pad_right: I64 -> U8 -> String -> String;
pad_right = |size, c, str| (
    let size = max(0, size);
    if size <= str.get_size { str };
    str + StringEx::fill(size - str.get_size, c)
);

// `input.replace_all(from, to)` replaces all occurrences of `from` in the input string with `to`.
// If `from` is empty, returns the input string unchanged.
//
// # Parameters
// * `from` - a string to be searched
// * `to` - a string to replace with
// * `input` - a string to be replaced
//
// Returns the modified string.
// If `from` is empty, returns the input string unchanged.
// If `from` is not found, returns the input string unchanged.
// If `to` is empty, removes all occurrences of `from` from the input string.
// If `input` is empty, returns an empty string.
//
// Example:
// ```
// "foo1:11,foo2:22,Foo3:33".replace_all("foo", "piyo")
// ==> "piyo1:11,piyo2:22,Foo3:33"
// "foo1:11,foo2:22,Foo3:33".replace_all("foo", "")
// ==> "1:11,2:22,Foo3:33"
// "foo1:11,foo2:22,Foo3:33".replace_all("", "piyo")
// ==> "foo1:11,foo2:22,Foo3:33"
// ```
replace_all: String -> String -> String -> String;
replace_all = |rep_from, rep_to, input| (
    if rep_from.get_size <= 0 {
        input
    };
    let output = Array::empty(input.get_size);
    let begin = 0;
    let end = input.get_size;
    loop(
        (output, begin), |(output, begin)|
        let pos = input.find(rep_from, begin);
        if pos.is_none {
            let output = output._append_range(input.get_bytes, begin, end);
            break $ output._unsafe_to_string
        };
        let pos = pos.as_some;
        let output = output._append_range(input.get_bytes, begin, pos);
        let output = output._append_range(rep_to.get_bytes, 0, rep_to.get_size);
        let begin = pos + rep_from.get_size;
        continue $ (output, begin)
    )
);

// (Internal function)
_append_range: Array a -> I64 -> I64 -> Array a -> Array a;
_append_range = |src_arr, src_begin, src_end, dest_arr| (
    if src_begin >= src_end { dest_arr };
    let dest_arr = dest_arr.push_back(src_arr.@(src_begin));
    _append_range(src_arr, src_begin + 1, src_end, dest_arr)
);

// `str.replace_suffix(from, to)` replaces `from` at the end of `str` with `to`.
// if `str` does not end with `from`, an error occurs.
//
// # Parameters
// * `from` - a string to be searched
// * `to` - a string to replace with
// * `str` - a string to be replaced
//
// Returns `ok(str)` if the replacement is successful.
// Returns `err("suffix does not match: str")` if the replacement fails.
//
// Example:
// ```
// "test.txt".replace_suffix(".txt", ".tmp")
//  ==> ok("test.tmp")
// "test.jpg".replace_suffix(".txt", ".tmp")
//  ==> err("suffix does not match: test.jpg")
// ```
replace_suffix: String -> String -> String -> Result ErrMsg String;
replace_suffix = |from, to, str| (
    if !str.ends_with(from) {
        err $ "suffix does not match: " + str
    };
    let str = str.substring(0, str.get_size - from.get_size);
    ok $ str + to
);

// `str.split_first(delim)` splits the string `str` into two parts at the first occurence of the delimiter `delim`.
// Returns `(left, right)` where `left` is the left part of the delimiter, and
// `right` is the right part of the delimiter.
// Returns `(str, "")` if the delimiter is not found.
//
// # Parameters
// * `delim` - a string to be searched
// * `str` - a string to be split
//
// Returns a tuple of two strings.
// If `delim` is empty, returns `(str, "")`.
// If `delim` is not found, returns `(str, "")`.
// If `str` is empty, returns `("", "")`.
//
// Example:
// ```
// "aaa/bbb/ccc".split_first("/")
// ==> ("aaa", "bbb/ccc")
// "aaa/bbb/ccc".split_first("bb")
// ==> ("aaa/", "b/ccc")
// "aaa/bbb/ccc".split_first("!")
// ==> ("aaa/bbb/ccc", "")
// ```
split_first: String -> String -> (String, String);
split_first = |delim, str| (
    let pos = str.find(delim, 0);
    if pos.is_none {
        (str, "")
    };
    let pos = pos.as_some;
    (str.substring(0, pos), str.substring(pos + delim.get_size, str.get_size))
);

// Deprecated: Please use `Std::String::split`.
//
// Same as Std::String::split, except that `"foo".split_ex(",")` returns a singleton iterator of "foo".
//
// Example:
// ```
// "foo,bar,baz".split_ex(",").to_array
// ==> ["foo", "bar", "baz"]
// "foo".split_ex(",").to_array
// ==> ["foo"]
// ```
split_ex: String -> String -> DynIterator String;
split_ex = |delim, str| (
    if delim == "" {
        Iterator::empty.push_front(str).to_dyn
    };
    let pos = str.find(delim, 0);
    if pos.is_none {
        Iterator::empty.push_front(str).to_dyn
    };
    str.split(delim).to_dyn
);

// Splits a string by a function that checks whether a character is a delimiter or not.
// The result will not contain any empty string.
//
// # Parameters
// * `is_delim` - a function that checks whether a character is a delimiter or not
// * `str` - a string to be split
//
// Returns a dynamic iterator of strings.
//
// Example:
// ```
// "  aa bb  12 ".split_by(Character::is_space).to_array
// ==>  ["aa", "bb", "12"]
// ```
split_by: (U8 -> Bool) -> String -> DynIterator String;
split_by = |is_delim, str| (
    let output = Array::empty(str.get_size);
    let output = loop(
        (output, 0, 0), |(output, start, i)|
        if i >= str.get_size {
            let output = if start < i { output.push_back(str.substring(start, i)) } else { output };
            break $ output
        };
        let c = str.get_bytes.@(i);
        if !is_delim(c) {
            continue $ (output, start, i + 1)
        };
        let output = if start < i { output.push_back(str.substring(start, i)) } else { output };
        continue $ (output, i + 1, i + 1)
    );
    output.to_iter.to_dyn
);

// Deprecated: Please use `Std::String::get_sub`.
//
// Returns a substring extracted from a specified range from a string.
// If the specified range exceeds the string, it will be truncated to fit within the string.
//
// Example:
// ```
// "abcdef".substring(2, 3)
// ==> "cde"
// "abcdef".substring(0, 6)
// ==> "abcdef"
// "abcdef".substring(-1, 7)
// ==> "abcdef"
// ```
substring: I64 -> I64 -> String -> String;
substring = |s, e, str| (
    let size = str.get_size;
    let s = max(0, min(size, s));
    let e = max(s, min(size, e));
    if s == 0 && e == size { str };
    str.get_sub(s, e)
);

// Deprecated: Please use `Std::Array::get_sub`.
//
// Returns a subarray extracted from a specified range from an array.
// If the specified range exceeds the array, it will be truncated to fit within the array.
//
// Example:
// ```
// [10, 11, 12, 13, 14].subarray(1, 2)
// ==> [11, 12]
// [10, 11, 12, 13, 14].subarray(0, 5)
// ==> [10, 11, 12, 13, 14]
// [10, 11, 12, 13, 14].subarray(-1, 6)
// ==> [10, 11, 12, 13, 14]
// ```
subarray: I64 -> I64 -> Array a -> Array a;
subarray = |s, e, arr| (
    let size = arr.get_size;
    let s = max(0, min(size, s));
    let e = max(s, min(size, e));
    if s == 0 && e == size { arr };
    arr.get_sub(s, e)
);

/*
// Obsolete: Use Std::String::starts_with, Std::String::ends_with.

// Checks if a string starts with the specified prefix.
//
// # Parameters
// * `prefix` - a string to be searched
// * `str` - a string to be checked
//
// Returns `true` if `str` starts with `prefix`, otherwise returns `false`.
// If `prefix` is empty, returns `true`.
//
// Example:
// ```
// "/foo/bar.txt".starts_with("/")
// ==> true
// "foo/bar.txt".starts_with("/")
// ==> false
// ```
starts_with: String -> String -> Bool;
starts_with = |prefix, str| (
    str.substring(0, prefix.get_size) == prefix
);

// Checks if a string ends with the specified suffix.
//
// # Parameters
// * `suffix` - a string to be searched
// * `str` - a string to be checked
//
// Returns `true` if `str` ends with `suffix`, otherwise returns `false`.
// If `suffix` is empty, returns `true`.
//
// Example:
// ```
// "/foo/bar.txt".ends_with(".txt")
// ==> true
// "foo/bar.txt".ends_with(".bin")
// ==> false
// ```
ends_with: String -> String -> Bool;
ends_with = |suffix, str| (
    str.substring(str.get_size - suffix.get_size, str.get_size) == suffix
);
*/

//---------------------------------
// Format
//---------------------------------

// Obsolete: please use `format`.
//formatv: String -> Array String -> String;
//formatv = format;

// `args.format(str)` replaces each occurence of `{}` in the format string `str`
// with each element of `args`.
// Currently only supports `{}`.
//
// # Parameters
// * `str` - a format string
// * `args` - a collection of elements
//
// Returns a formatted string.
//
// Example:
// ```
// ["1", "2", "3"].format("foo={} bar={} baz={}")
// ==> "foo=1 bar=2 baz=3"
// ["1"].format("foo={} bar={} baz={}")
// ==> "foo=1 bar={} baz={}"
// ```
trait a: Format {
    format: String -> a -> String;
}

// `array.format(str)` replaces each occurence of `{}` in the format string `str`
// with each element of `array`.
// Currently only supports `{}`.
//
// Example:
// ```
// [some(1), some(2), none()].format("foo={} bar={} baz={}")
// ==> "foo=some(1) bar=some(2) baz=none()"
// [some(1)].format("foo={} bar={} baz={}")
// ==> "foo=some(1) bar={} baz={}"
// ```
impl [a: ToString] Array a: Format {
    format = |str, args| (
        let from = "{}";
        let output = Array::empty(args.get_size * 2);
        let (output, tail) = args.to_iter.loop_iter(
            (output, 0), |arg, (output, find_start)|
            let pos = str.find(from, find_start);
            if pos.is_none {
                break $ (output, find_start)
            };
            let pos = pos.as_some;
            let output = output
                .push_back(str.substring(find_start, pos))
                .push_back(arg.to_string)
            ;
            let find_start = pos + from.get_size;
            continue $ (output, find_start)
        );
        let output = output
            .push_back(str.substring(tail, str.get_size));
        output.to_iter.concat_iter
    );
}

// `(a, ).format(str)` replaces an occurence of `{}` in the format string `str`
// with `a`.
//
// Example:
// ```
// (12.345, ).format("float={}")
// ==> "float=12.345000"
// ([1, 2, 3], ).format("arr={}")
// ==> "arr=[1, 2, 3]"
// ```
impl [a: ToString] (a, ): Format {
    format = |str, (a,)| [a.to_string].format(str);
}

// `(a, b).format(str)` replaces each occurence of `{}` in the format string `str`
// with `a`, `b`.
//
// Example:
// ```
// (12, "abc").format("int={} str={}")
// ==> "int=12 str=abc"
// ```
impl [a: ToString, b: ToString] (a,b): Format {
    format = |str, (a,b)| [a.to_string, b.to_string].format(str);
}

// `(a, b, c).format(str)` replaces each occurence of `{}` in the format string `str`
// with `a`, `b`, `c`.
//
// Example:
// ```
// (12, 345.678, "abc").format("int={} float={} str={}")
// ==> "int=12 float=345.678000 str=abc"
// ```
impl [a: ToString, b: ToString, c:ToString] (a,b,c): Format {
    format = |str, (a,b,c)| [a.to_string, b.to_string, c.to_string].format(str);
}

// `(a, b, c, d).format(str)` replaces each occurence of `{}` in the format string `str`
// with `a`, `b`, `c`, `d`.
//
// Example:
// ```
// (12, 345.678, "abc", some(1)).format("int={} float={} str={} option={}")
// ==> "int=12 float=345.678000 str=abc option=some(1)"
// ```
impl [a: ToString, b: ToString, c:ToString, d:ToString] (a,b,c,d): Format {
    format = |str, (a,b,c,d)| [a.to_string, b.to_string, c.to_string, d.to_string].format(str);
}

// `(a, b, c, d, e).format(str)` replaces each occurence of `{}` in the format string `str`
// with `a`, `b`, `c`, `d`, `e`.
//
// Example:
// ```
// (12, 345.678, "abc", some(1), [1, 2, 3]).format("int={} float={} str={} option={} array={}")
// ==> "int=12 float=345.678000 str=abc option=some(1) array=[1, 2, 3]"
// ```
impl [a: ToString, b: ToString, c:ToString, d:ToString, e:ToString] (a,b,c,d,e): Format {
    format = |str, (a,b,c,d,e)| [a.to_string, b.to_string, c.to_string, d.to_string, e.to_string].format(str);
}

