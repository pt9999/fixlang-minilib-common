module ErrorTest;


import Minilib.Monad.Error;
import Minilib.Testing.UnitTest;

test_result_error: TestCase;
test_result_error = (
    make_test("test_result_error") $ |_|
    let f: I64 -> Result ErrMsg I64 = |i| (
        if i == 1 { error $ "err1" };
        if i == 2 { error $ "err2" };
        pure $ i
    );
    assert_equal("ok0", ok(0), f(0));;
    assert_equal("err1", err("err1"), f(1));;
    assert_equal("err2", err("err2"), f(2));;
    let handler = |errmsg| (
        if errmsg == "err1" { ok $ "catch: " + errmsg }
        else { err $ "rethrow: " + errmsg }
    );
    let f2 = |i| f(i).map(|ans| "ok: " + ans.to_string).catch(handler);
    assert_equal("catch ok0", ok $ ("ok: 0"), f2(0));;
    assert_equal("catch err1", ok $ ("catch: err1"), f2(1));;
    assert_equal("catch err2", err $ ("rethrow: err2"), f2(2));;
    pure()
);

test_iofail_error: TestCase;
test_iofail_error = (
    make_test("test_iofail_error") $ |_|
    let f: I64 -> IOFail I64 = |i| (
        if i == 1 { error $ "err1" };
        if i == 2 { error $ "err2" };
        pure $ i + 10
    );
    assert_equal("ok0", ok(10), *f(0).to_result.lift);;
    assert_equal("err1", err("err1"), *f(1).to_result.lift);;
    assert_equal("err2", err("err2"), *f(2).to_result.lift);;
    let handler: String -> IOFail I64 = |errmsg| (
        if errmsg == "err1" { pure $ 20 };
        error $ errmsg
    );
    let f2 = |i| f(i).catch(handler);
    assert_equal("catch ok0", ok $ 10, *f2(0).to_result.lift);;
    assert_equal("catch err1", ok $ 20, *f2(1).to_result.lift);;
    assert_equal("catch err2", err $ "err2", *f2(2).to_result.lift);;
    pure()
);

test_result_finally: TestCase;
test_result_finally = (
    make_table_test("test_result_finally",
        [
            (ok(42), ok(), ok(42)),
            (ok(42), err("cleanup"), err("cleanup")),
            (err("body"), ok(), err("body")),
            (err("body"), err("cleanup"), err("cleanup")),
        ]: Array (Result ErrMsg I64, Result ErrMsg (), Result ErrMsg I64),
        |(body, cleanup, expected)|
        let actual = body.finally(cleanup);
        assert_equal("eq", expected, actual)
    )
);

test_iofail_finally: TestCase;
test_iofail_finally = (
    make_table_test("test_iofail_finally",
        [
            (ok(42), ok(), ok(42)),
            (ok(42), err("cleanup"), err("cleanup")),
            (err("body"), ok(), err("body")),
            (err("body"), err("cleanup"), err("cleanup")),
        ]: Array (Result ErrMsg I64, Result ErrMsg (), Result ErrMsg I64),
        |(body, cleanup, expected)|
        let body: IOFail I64 = body.from_result;
        let cleanup: IOFail () = cleanup.from_result;
        let actual = *body.finally(cleanup).to_result.lift;
        assert_equal("eq", expected, actual)
    )
);

test_result_catch_finally: TestCase;
test_result_catch_finally = (
    make_table_test("test_result_finally",
        [
            (ok(42), ok(100), ok(), ok(42)),
            (ok(42), err("handler"), ok(), ok(42)),
            (ok(42), ok(100), err("cleanup"), err("cleanup")),
            (ok(42), err("handler"), err("cleanup"), err("cleanup")),
            (err("body"), ok(100), ok(), ok(100)),
            (err("body"), err("handler"), ok(), err("handler")),
            (err("body"), ok(100), err("cleanup"), err("cleanup")),
            (err("body"), err("handler"), err("cleanup"), err("cleanup")),
        ]: Array (Result ErrMsg I64, Result ErrMsg I64, Result ErrMsg (), Result ErrMsg I64),
        |(body, handler, cleanup, expected)|
        let actual = body.catch(|errmsg| handler).finally(cleanup);
        assert_equal("eq", expected, actual)
    )
);

test_iofail_catch_finally: TestCase;
test_iofail_catch_finally = (
    make_table_test("test_iofail_catch_finally",
        [
            (ok(42), ok(100), ok(), ok(42)),
            (ok(42), err("handler"), ok(), ok(42)),
            (ok(42), ok(100), err("cleanup"), err("cleanup")),
            (ok(42), err("handler"), err("cleanup"), err("cleanup")),
            (err("body"), ok(100), ok(), ok(100)),
            (err("body"), err("handler"), ok(), err("handler")),
            (err("body"), ok(100), err("cleanup"), err("cleanup")),
            (err("body"), err("handler"), err("cleanup"), err("cleanup")),
        ]: Array (Result ErrMsg I64, Result ErrMsg I64, Result ErrMsg (), Result ErrMsg I64),
        |(body, handler, cleanup, expected)|
        let body: IOFail I64 = body.from_result;
        let handler: IOFail I64 = handler.from_result;
        let cleanup: IOFail () = cleanup.from_result;
        let actual = *body.catch(|errmsg| handler).finally(cleanup).to_result.lift;
        assert_equal("eq", expected, actual)
    )
);

main: IO ();
main = (
    [
        test_result_error,
        test_iofail_error,
        test_result_finally,
        test_iofail_finally,
        test_result_catch_finally,
        test_iofail_catch_finally,
    ]
    .run_test_driver
);

